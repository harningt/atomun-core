buildscript {
    ext.kotlin_version = "1.3.41"
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:6.0.3"
        classpath "us.eharning.gradle:gradle-defaults:2.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
}

apply plugin: 'kotlin'
apply plugin: 'us.eharning.gradle.defaults'

group = 'us.eharning.atomun'
description = 'Utility library implementing core support functionality.'
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
}

spotbugs {
    toolVersion = '4.0.0-beta1'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compile 'net.jcip:jcip-annotations:1.0'
    compile 'com.github.spotbugs:spotbugs-annotations:4.0.0-beta4'
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    compile 'org.bouncycastle:bcprov-jdk15on:1.62'

    testCompile 'org.codehaus.groovy:groovy:2.5.8'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
    testCompile 'net.trajano.commons:commons-testing:2.1.0'
    testCompile 'org.yaml:snakeyaml:1.25'
}

configurations.all {
    resolutionStrategy {
        resolutionStrategy.dependencySubstitution {
            substitute(module("dom4j:dom4j"))
                    .because("Use newer dom4j packages")
                    .with(module("org.dom4j:dom4j:2.1.1"))

        }
        eachDependency { DependencyResolveDetails details ->
            //specifying a fixed version for groovy
            if (details.requested.group == 'org.codehaus.groovy') {
                details.useVersion '2.5.7'
            }
        }
    }
}

test {
    /* If in JDK 9 or higher, must mark illegal-access as denied */
    if (JavaVersion.current() > JavaVersion.VERSION_1_8) {
        jvmArgs('--illegal-access=deny')
    }
}

javadoc {
    options.tags "todo:a:TODO:"
}

apply from: "$rootDir/gradle/maven-publish.gradle"

wrapper {
    gradleVersion = '5.4.1'
}

/* Sanity check that the minimum class-set is from Java 7 */
task proguardCheck(type: proguard.gradle.ProGuardTask, dependsOn: assemble) {
    injars project.jar.archivePath
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/rt.jar")
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/jce.jar")
    libraryjars files(configurations.compile)
    // Don't really try to do anything, we just want this tool for it's bytecode lint
    dontshrink
    dontobfuscate
    dontoptimize
    // Don't fail if LambdaMetafactory is missing - it would be handled by Retrolambda
    dontwarn 'java.lang.invoke.LambdaMetafactory'
    // Don't note that these are duplicately imported, different test tools pull in
    dontnote 'javax.annotation.**'
    dontnote 'net.jcip.annotations.**'
    dontwarn 'module-info'
}
