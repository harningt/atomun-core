buildscript {
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:6.0.2"
    }
}

plugins {
    id 'java'
    id 'groovy'
    id "org.jetbrains.kotlin.jvm"  version '1.2.41'
    id 'maven'
    id 'us.eharning.gradle.defaults' version '1.0.0'
}

group = 'us.eharning.atomun'
description = 'Utility library implementing core support functionality.'
sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    findbugs 'com.google.code.findbugs:findbugs:3.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compile 'com.google.code.findbugs:annotations:3.0.1u2'
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    compile 'org.bouncycastle:bcprov-jdk15on:1.59'

    testCompile 'org.codehaus.groovy:groovy:2.4.14'
    testCompile 'cglib:cglib-nodep:3.2.6'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'net.trajano.commons:commons-testing:2.1.0'
    testCompile 'net.trajano.caliper:caliper:1.2.1'
    testCompile 'org.yaml:snakeyaml:1.21'
}

javadoc {
    options.tags "todo:a:TODO:"
}

apply from: "$rootDir/gradle/maven-publish.gradle"

wrapper {
    gradleVersion = '4.9'
}

/* Sanity check that the minimum class-set is from Java 7 */
task proguardCheck(type: proguard.gradle.ProGuardTask, dependsOn: assemble) {
    injars project.jar.archivePath
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/rt.jar")
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/jce.jar")
    libraryjars files(configurations.compile)
    // Don't really try to do anything, we just want this tool for it's bytecode lint
    dontshrink
    dontobfuscate
    dontoptimize
    // Don't fail if LambdaMetafactory is missing - it would be handled by Retrolambda
    dontwarn 'java.lang.invoke.LambdaMetafactory'
    // Don't note that these are duplicately imported, different test tools pull in
    dontnote 'javax.annotation.**'
    dontnote 'net.jcip.annotations.**'
}
